# Tests the logic analysing model annotations required by the protocol.
# Also tests analysis of the protocol's own interface (inputs & outputs).

namespace oxmeta = "https://chaste.comlab.ox.ac.uk/cellml/ns/oxford-metadata#"
namespace local = "urn:fc:local#"

units {
    mV = milli volt
    ms = milli second
    uA_per_cm2 = micro ampere . centi metre^-2 "{/Symbol m}A/cm^2" # Desired units of currents
    uF_per_cm2 = micro farad . centi metre^-2
    microamps = micro ampere
    A_per_F = ampere . farad^-1
    mM = milli mole . litre^-1 "{/Symbol m}M"
}

model interface {
    # Simple required outputs, inputs & clamps
    output oxmeta:time units ms
    input oxmeta:extracellular_sodium_concentration units mM
    clamp oxmeta:cytosolic_sodium_concentration

    # Optional input because it's defined completely
    input oxmeta:cytosolic_potassium_concentration units mM = 0.0

    # Explicitly optional outputs, inputs & clamps
    output oxmeta:membrane_fast_sodium_current units uA_per_cm2
    optional oxmeta:membrane_fast_sodium_current
    input oxmeta:extracellular_calcium_concentration units mM
    optional oxmeta:extracellular_calcium_concentration
    clamp oxmeta:cytosolic_calcium_concentration
    optional oxmeta:cytosolic_calcium_concentration

    # Variables used in defaults are also optional
    output oxmeta:membrane_rapid_delayed_rectifier_potassium_current
    optional oxmeta:membrane_rapid_delayed_rectifier_potassium_current default oxmeta:membrane_inward_rectifier_potassium_current

    # Optional because it's defined by an equation
    output local:newvar units mV
    define local:newvar = 1 :: mV

    # Required because no initial value given
    output oxmeta:membrane_voltage units mV
    define diff(oxmeta:membrane_voltage; oxmeta:time) = 1 :: mV_per_ms

    # Optional terms in unit conversions, unless used elsewhere
    convert microamps to uA_per_cm2 by lambda rhs: (rhs / oxmeta:membrane_capacitance) * 1 :: uF_per_cm2
    convert mV to mM by lambda rhs: rhs / 1 :: mV * local:funky_converter
    define oxmeta:extracellular_potassium_concentration = local:funky_converter
}

tasks {
    # A fake simulation so we can access model variables later
    simulation sim = timecourse {
        range t units ms uniform 0:2
    }
}

post-processing {
    pp_end_time = sim:time[-1]
}

outputs {
    # Uses units taken from model interface
    model_interface_units = sim:time

    # Uses units defined here
    pp_defined_units units ms

    # Simulation output with units defined here
    sim_defined_units = sim:time units second

    # Units that aren't defined
    missing_units = sim:newvar units missing

    # Units not defined in model interface, so unknown until applied to a model
    unknown_units = sim:membrane_rapid_delayed_rectifier_potassium_current
}
